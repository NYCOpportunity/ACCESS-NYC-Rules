package accessnyc.rules;

import accessnyc.response.EligibleProgram
import accessnyc.rules.income.IncomeAdultsChildrenTotalMonthly
import accessnyc.rules.income.IncomePersonMonthly 
import accessnyc.rules.income.IncomePersonItemMonthly 
import accessnyc.request.Household
import accessnyc.request.Person

rule "s2_r003.3"
    dialect "mvel"
    
    when 
        Household(city == "NYC") 
        Person(age < 3, headOfHouseholdRelation == "FosterChild")
    then
        $programcode = new EligibleProgram()
        $programcode.setCode("S2R003")
        insertLogical($programcode)

end

rule "s2_r003.4"
    dialect "mvel"
    
    when 
        Household(city == "NYC") 
        Person(age < 3)
        IncomePersonItemMonthly(type in ("SSI", "CashAssistance"), (person.headOfHousehold == true || person.headOfHouseholdRelation == "Spouse"))
    then
        $programcode = new EligibleProgram()
        $programcode.setCode("S2R003")
        insertLogical($programcode)

end

rule "s2_r003.5"
    dialect "mvel"
    when 
        Household(city == "NYC") 
        Person( age < 3, headOfHouseholdRelation in ("Child","StepChild")) 

        ( 
          (Household(members == 2)  and IncomeAdultsChildrenTotalMonthly(amount <= 5624)) or
          (Household(members == 3)  and IncomeAdultsChildrenTotalMonthly(amount <= 6948)) or 
          (Household(members == 4)  and IncomeAdultsChildrenTotalMonthly(amount <= 8271)) or 
          (Household(members == 5)  and IncomeAdultsChildrenTotalMonthly(amount <= 9594)) or 
          (Household(members == 6)  and IncomeAdultsChildrenTotalMonthly(amount <= 10918)) or 
          (Household(members == 7)  and IncomeAdultsChildrenTotalMonthly(amount <= 11166)) or
          (Household(members == 8)  and IncomeAdultsChildrenTotalMonthly(amount <= 11414))
        ) 
        
    then
        $programcode = new EligibleProgram()
        $programcode.setCode("S2R003")
        insertLogical($programcode)

end


rule "s2_r003.6"
    dialect "mvel"
    
    when 
        Household(city == "NYC") 
        IncomePersonMonthly(person.age < 3, person.headOfHouseholdRelation not in ("Child","StepChild"), amount <= 4301)
        
    then
        $programcode = new EligibleProgram()
        $programcode.setCode("S2R003")
        insertLogical($programcode)

end