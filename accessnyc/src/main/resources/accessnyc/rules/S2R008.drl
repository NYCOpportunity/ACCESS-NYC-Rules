package accessnyc.rules;

import accessnyc.response.EligibleProgram

import accessnyc.request.Person
import accessnyc.request.Household

import accessnyc.rules.income.IncomeHouseholdTotalYearly
import accessnyc.rules.income.IncomeHouseholdHasCashAssistance
import accessnyc.rules.income.IncomeHouseholdHasSSI
import accessnyc.rules.household.FosterChildren

rule "s2_r008 by income"
    dialect "mvel"
    
    when 
        Household(city == "NYC")
        Person( age < 5, age > 2 )
        
        (
               (Household(members == 1) and IncomeHouseholdTotalYearly(amount <= 15060))
            or (Household(members == 2) and IncomeHouseholdTotalYearly(amount <= 20440))
            or (Household(members == 3) and IncomeHouseholdTotalYearly(amount <= 25820))
            or (Household(members == 4) and IncomeHouseholdTotalYearly(amount <= 31200))
            or (Household(members == 5) and IncomeHouseholdTotalYearly(amount <= 36580))
            or (Household(members == 6) and IncomeHouseholdTotalYearly(amount <= 41960))
            or (Household(members == 7) and IncomeHouseholdTotalYearly(amount <= 47340))
            or (Household(members == 8) and IncomeHouseholdTotalYearly(amount <= 52720))
        )

    then
        $programcode = new EligibleProgram()
        $programcode.setCode("S2R008")
        insertLogical($programcode)

end

rule "s2_r008 Cash Assistance/SSI"
    dialect "mvel"
    
    when 
        Household(city == "NYC")
        (IncomeHouseholdHasCashAssistance(value == true) or IncomeHouseholdHasSSI(value == true))

    then
        $programcode = new EligibleProgram()
        $programcode.setCode("S2R008")
        insertLogical($programcode)

end

rule "s2_r008 has foster child"
    dialect "mvel"
    
    when 
        Household(city == "NYC")
        FosterChildren(value > 0)

    then
        $programcode = new EligibleProgram()
        $programcode.setCode("S2R008")
        insertLogical($programcode)

end