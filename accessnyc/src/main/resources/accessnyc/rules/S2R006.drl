package accessnyc.rules;

import accessnyc.response.EligibleProgram

import accessnyc.request.Person
import accessnyc.request.Household

import accessnyc.rules.household.HeadOfHouseholdMarried
import accessnyc.rules.household.ChildrenStudentEITCDefinition
import accessnyc.rules.household.BlindDisabledHouseholdEITC
import accessnyc.rules.income.IncomeHeadAndSpouseEarnedYearly
import accessnyc.rules.income.IncomeHeadAndSpouseInvestmentYearly
import accessnyc.rules.income.IncomeHeadEarnedYearly
import accessnyc.rules.income.IncomeHeadInvestmentYearly
import accessnyc.rules.income.IncomePersonEarnedYearly
import accessnyc.rules.income.IncomePersonInvestmentYearly


rule "s2_r006.2 head of household or spouse - children and students under 24 - childrenstudenteitc >= 1"
    dialect "mvel"
    
    when
        HeadOfHouseholdMarried( value == true )
        (
            Person( headOfHousehold == true ) 
            or Person( headOfHouseholdRelation == "Spouse")
        ) 
        ChildrenStudentEITCDefinition( value >= 1 ) 
        IncomeHeadAndSpouseEarnedYearly( amount > 0  , amount <= 59187 )
        IncomeHeadAndSpouseInvestmentYearly( amount <= 10300 )


    then
        $programcode = new EligibleProgram()
        $programcode.setCode("S2R006")
        $programcode.setRulePart("2")
        insertLogical($programcode)
end

rule "s2_r006.3 head of household"
    dialect "mvel"
    
    when
        HeadOfHouseholdMarried( value == true )
        (
            Person( headOfHousehold == true, age >= 25, age < 65 ) 
            or Person( headOfHouseholdRelation == "Spouse", age >= 25, age < 65 )
        )
        IncomeHeadAndSpouseEarnedYearly( amount > 0, amount <= 22610 )
        IncomeHeadAndSpouseInvestmentYearly( amount <= 10300 )

    then
        $programcode = new EligibleProgram()
        $programcode.setCode("S2R006")
        $programcode.setRulePart("3")
        insertLogical($programcode)
end


rule "s2_r006.4 child under 19 or student under 24"
    dialect "mvel"
    
    when
        $person : Person()
        not HeadOfHouseholdMarried( value == true )
        ChildrenStudentEITCDefinition( value > 0 )
        IncomePersonEarnedYearly( person == $person, person.headOfHousehold == true, person.blind == false, person.disabled == false, amount > 0, amount <= 53057 )
        IncomePersonInvestmentYearly( person == $person, person.headOfHousehold == true, person.blind == false, person.disabled == false, amount <= 10300 )

    then
        $programcode = new EligibleProgram()
        $programcode.setCode("S2R006")
        $programcode.setRulePart("4")
        insertLogical($programcode)
end

rule "s2_r006.5"
    dialect "mvel"
    
    when
        $person : Person()
        not HeadOfHouseholdMarried( value == true )
        IncomeHeadEarnedYearly( person == $person, person.headOfHousehold == true, person.age >= 25, person.age < 65, amount > 0, amount <= 16480 )
        IncomeHeadInvestmentYearly( person == $person, person.headOfHousehold == true, person.age >= 25, person.age < 65, amount <= 10300 )

    then
        $programcode = new EligibleProgram()
        $programcode.setCode("S2R006")
        $programcode.setRulePart("5")
        insertLogical($programcode)
end

