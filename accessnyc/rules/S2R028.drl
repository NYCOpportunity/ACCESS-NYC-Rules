package accessnyc.rules;

import accessnyc.response.EligibleProgram

import accessnyc.request.Person
import accessnyc.request.Household
import accessnyc.rules.income.IncomeHouseholdHasCashAssistance
import accessnyc.rules.income.IncomeHouseholdHasSSI
import accessnyc.rules.household.MembersNuclearOnly
import accessnyc.rules.income.IncomeHouseholdNuclearISYYearly
import accessnyc.rules.income.IncomePersonISYYearly

rule "S2R028.3"
    dialect "mvel"
    
    when 
        Household( city == "NYC", livingShelter == true)
        Person( age >= 14, age <= 21 ) 
    then
        $programcode = new EligibleProgram()
        $programcode.setCode("S2R028")
        insertLogical($programcode)

end

rule "S2R028.4"
    dialect "mvel"
    
    when 
        Household( city == "NYC")
        // Person is in foster care
        (
            Person(age >= 14, age <= 21, headOfHouseholdRelation == "FosterChild")
            or (Person(age >= 14, age <= 21, headOfHousehold == true) and Person(headOfHouseholdRelation == "FosterParent"))
        )        
    then
        $programcode = new EligibleProgram()
        $programcode.setCode("S2R028")
        insertLogical($programcode)

end

rule "S2R028.5"
    dialect "mvel"
    
    when 
        Household( city == "NYC")
        (
            Person( age >= 14, age <= 21, disabled == true )
            or Person( age >= 14, age <= 21, blind == true )
        )
        
    then
        $programcode = new EligibleProgram()
        $programcode.setCode("S2R028")
        insertLogical($programcode)

end

rule "S2R028.6"
    dialect "mvel"
    
    when 
        Household( city == "NYC")
        // Person is pregnant or a parent
        (
            Person(age >= 14, age <= 21, pregnant == true)
            or Person(age >= 14, age <= 21, headOfHouseholdRelation == "Parent")
            or (Person(age >= 14, age <= 21, headOfHousehold == true) and Person(headOfHouseholdRelation == "Child"))
        )        
    then
        $programcode = new EligibleProgram()
        $programcode.setCode("S2R028")
        insertLogical($programcode)

end

rule "S2R028.7"
    dialect "mvel"
    
    when 
        Household( city == "NYC")
        Person( age >= 14, age <= 21 )
        IncomeHouseholdHasCashAssistance(value == true) or IncomeHouseholdHasSSI(value == true)
        
    then
        $programcode = new EligibleProgram()
        $programcode.setCode("S2R028")
        insertLogical($programcode)

end

rule "S2R028.8"
    dialect "mvel"
    
    when 
        Household( city == "NYC")
        Person( age >= 14, age <= 21 )
        (
           (Household(members == 1) and IncomeHouseholdTotalYearly(amount <= 15060))
        or (Household(members == 2) and IncomeHouseholdTotalYearly(amount <= 20440))
        or (Household(members == 3) and IncomeHouseholdTotalYearly(amount <= 25820))
        or (Household(members == 4) and IncomeHouseholdTotalYearly(amount <= 31200))
        or (Household(members == 5) and IncomeHouseholdTotalYearly(amount <= 36580))
        or (Household(members == 6) and IncomeHouseholdTotalYearly(amount <= 41960))
        or (Household(members == 7) and IncomeHouseholdTotalYearly(amount <= 47340))
        or (Household(members == 8) and IncomeHouseholdTotalYearly(amount <= 52720))
        )
    then
        $programcode = new EligibleProgram()
        $programcode.setCode("S2R028")
        insertLogical($programcode)

end
